'TIP: You can encrypt this file when it is saved, or password protect it, by adding a special comment to the first line of the file (with no leading spaces).
'To encrypt the file, set the comment on the first line to: 'ENCRYPTED
'To password protect the file (which will also encrypt it), set the comment on the first line to: 'PASSWORD:your_password_phrase
'To create a compiled AEX library (when this project is published), add the comment 'COMPILEAEX at the START of a line in the code.

' Input: e.tableName
' Input: [e.columns]
' Input: [e.filters] like {"columnName": name, columnVal: val}
' Input: e.pageOptions like {pageSize: n, firstIdNotSeen: n} or nothing
function getTableData (e as p) as c
	
	if json_get_array_length(e.columns) = 0 then 
		getTableData = ";"
		end 
	end if
	
	dim filterText as c
	filterText = *html_unescape(e.filters, "utf8")
	
	dim filters as p
	filters = json_parse(filterText)
	
	dim args as sql::arguments
	dim filterStr as c
	filterStr = ""

	if filters.size() == 0 then 
		filterStr = "";
	else
		dim argCounter as n
		argCounter = 0
		for each filter in filters
			if filter.value.data("op") == "" then 
				filter.value.op = "="
			end if 
			
			if len(filterStr) != 0 then 
				filterStr = filterStr + " " + filter.value.connector + " "
			end if
			
			filterStr = filterStr + "[" + filter.value.columnName + "] " + filter.value.op + " :" + "argNum" + argCounter
			args.add("argNum" + argCounter, filter.value.columnVal);
			argCounter = argCounter + 1
		next filter
	end if
	
	
	dim columns as p
	columns = json_parse(e.columns)
	
	'DIM an Xbasic AlphaDAO connection object
	dim cn as sql::connection
	dim flag as l
	''open the connection object to connect to the Database
	flag = cn_open(cn,"::Name::conn")
	dim sql as c
	dim selectPart as c 
	dim fromPart as c
	dim wherePart as c
	
	selectPart = ""
	for each col in columns
		selectPart = selectPart + "[" + col.value + "],"
	next
	if right(selectPart, 1) == "," then 
		selectPart = substr(selectPart, 1, len(selectPart) - 1)
	end if
	
	fromPart = " [" + e.tableName + "]"
	
	if len(filterStr) <> 0 then 
		wherePart = " " + filterStr
	else 
		wherePart = " 1 = 1 "
	end if
	
	if e.data("pageOptions") = "" then 
		sql = "SELECT " + selectPart + " FROM " + fromPart + " WHERE " + wherePart
	else
		dim pageOptions as p
		pageOptions = json_parse(e.pageOptions)
		sql = buildPaginatedQuery(e.tableName, selectPart, fromPart, wherePart, pageOptions)
	end if
	
	'turn portable SQL on. (for more information on portable sql search in help (https://www.alphasoftware.com/documentation)
	cn.PortableSQLEnabled = .t.
	'execute the SQL - pass in the sql::arguments object
	flag = cn.execute(sql,args)
	if flag = .f. then 
		debug(1)
		'an error occurred - get the error message from cn.callresult.text 
		'report the error, close the database and then end the script
		dim errortext as c 
		errortext = js_escape(cn.callresult.text)
		getTableData = "console.error('" +  errortext + "');"
		cn.close()
		end
	end if
	'DIM a SQL::Resultset object
	dim rs as sql::resultset
	rs = cn.resultset
	'get the JSON from the query
	dim json as c 
	json = rs.toJSON()
	'Clean up by freeing the resultset and then closing the database
	cn.freeresult()
	cn.close()
	getTableData = "{dialog.object}.stateInfo.allListData = " + json + ";"
	
end function

' Input: Table Name
' Input: dirty (from 'harvestList')
function updateTableData (e as p) as c
	
	dim schemaInfo as p
	dim info as c
	dim schema as p
	schemaInfo = require("schemaInfo")
	info = schemaInfo.getConnectionSchemaJSON("conn", e.tableName)
	schema = json_parse(info)
	
	dim dirty as p
	dirty = json_parse(e.dirty)
	
	'DIM an Xbasic AlphaDAO connection object
	dim cn as sql::connection
	dim flag as l
	''open the connection object to connect to the Database
	flag = cn_open(cn,"::Name::conn")
	
	for each item in dirty
		
		dim shouldDelete as l
		dim shouldUpdate as l
		dim shouldInsert as l
		
		' I know this is a very long-winded way to write this, but in the future it might be helpful 
		' to have separate flags like this.
		
		if prop_valid(item.value, "_isDeleted") then 
			shouldDelete = item.value._isDeleted
		else if prop_valid(item.value, "_isNewRow") then 
			shouldInsert = item.value._isNewRow
		end if 
		
		if shouldDelete == .f. .and. shouldInsert == .f. then 
			shouldUpdate = .t.
		end if
		
		
		if shouldDelete then 
			runDeleteQuery(e, schema, item, cn)
		else if shouldUpdate then 
			runUpdateQuery(e, schema, item, cn)
		else if shouldInsert then
			runInsertQuery(e, schema, item, cn)
		end if
	next item
	
	updateTableData = ";"
	
end function

function runUpdateQuery as v (e as p, schema as p, item as p, cn as sql::connection)
	dim updateSql as c
	updateSql = "UPDATE [" + e.tableName + "] SET "
	dim args as sql::arguments
	
	' For each field, add an arg to the pile
	for each column in schema.jsonOutput.column
		if prop_valid(item.value, column.value.name) == .f. then 
			continue 
		end if
		
		if column.value.name == schema.primaryKey then 
			args.set(schema.primaryKey, convert_type(item.value.data(schema.primaryKey), column.value.alphaType))
			continue
		end if 
		
		args.set(column.value.name, convert_type(item.value.data(column.value.name), column.value.alphaType))
		updateSql = updateSql + "[" + column.value.name + "] = :" + column.value.name + ","

	next field
	
	if right(updateSql, 1) == "," then  
		updateSql = substr(updateSql, 1, len(updateSql) - 1)
	end if
	
	updateSql = updateSql + " WHERE [" + schema.primaryKey + "] = :" + schema.primaryKey
	
	'turn portable SQL on. (for more information on portable sql search in help (https://www.alphasoftware.com/documentation)
	cn.PortableSQLEnabled = .t.
	'execute the SQL - pass in the sql::arguments object
	flag = cn.execute(updateSql,args)
	if flag = .f. then 
		'an error occurred - get the error message from cn.callresult.text 
		'report the error, close the database and then end the script
		dim errortext as c 
		errortext = js_escape(cn.callresult.text)
		updateTableData = "console.error('" +  errortext + "');"
		cn.close()
		end
	end if
end function

function runInsertQuery as v (e as p, schema as p, item as p, cn as sql::connection)
	dim insertSql as c
	insertSql = "INSERT INTO [" + e.tableName + "] ("
	dim args as sql::arguments
	
	dim valueSql as c
	valueSql = " VALUES ("
	
	for each column in schema.jsonOutput.column
		if prop_valid(item.value, column.value.name) == .f. then 
			continue 
		end if
		
		if column.value.name == schema.primaryKey then
			continue
		end if 
		
		args.set(column.value.name, convert_type(item.value.data(column.value.name), column.value.alphaType))
		insertSql = insertSql + "[" + column.value.name + "],"
		valueSql = valueSql + ":" + column.value.name + ","

	next field
	
	if right(insertSql, 1) == "," then  
		insertSql = substr(insertSql, 1, len(insertSql) - 1)
	end if
	
	if right(valueSql, 1) == "," then  
		valueSql = substr(valueSql, 1, len(valueSql) - 1)
	end if
	
	insertSql = insertSql + ")"
	valueSql = valueSql + ")"
	
	insertSql = insertSql + valueSql
	
	'turn portable SQL on. (for more information on portable sql search in help (https://www.alphasoftware.com/documentation)
	cn.PortableSQLEnabled = .t.
	'execute the SQL - pass in the sql::arguments object
	flag = cn.execute(insertSql,args)
	if flag = .f. then 
		'an error occurred - get the error message from cn.callresult.text 
		'report the error, close the database and then end the script
		dim errortext as c 
		errortext = js_escape(cn.callresult.text)
		updateTableData = "console.error('" +  errortext + "');"
		cn.close()
		end
	end if
end function

function runDeleteQuery as v (e as p, schema as p, item as p, cn as sql::connection)
	dim deleteSql as c
	deleteSql = "DELETE FROM [" + e.tableName + "] WHERE [" + schema.primaryKey + "] = :" + schema.primaryKey
	
	dim args as sql::arguments 
	args.add(schema.primaryKey, item.value.data(schema.primaryKey))
	
	'turn portable SQL on. (for more information on portable sql search in help (https://www.alphasoftware.com/documentation)
	cn.PortableSQLEnabled = .t.
	'execute the SQL - pass in the sql::arguments object
	flag = cn.execute(deleteSql,args)
	if flag = .f. then 
		'an error occurred - get the error message from cn.callresult.text 
		'report the error, close the database and then end the script
		dim errortext as c 
		errortext = js_escape(cn.callresult.text)
		updateTableData = "console.error('" +  errortext + "');"
		cn.close()
		end
	end if
end function

' pageOptions = {pageSize: n, firstIdNotSeen: n}
function buildPaginatedQuery as c (tableName as c, selectPart as c, fromPart as c, wherePart as c, pageOptions as p)
	dim schemaInfo as p
	dim info as c
	dim schema as p
	dim primaryKey as c
	schemaInfo = require("schemaInfo")
	info = schemaInfo.getConnectionSchemaJSON("conn", tableName)
	schema = json_parse(info)
	primaryKey = schema.primaryKey
	
	dim sql as c 
	sql = "SELECT FIRST " + pageOptions.pageSize + " " + selectPart + " FROM " + fromPart + " WHERE (" + wherePart + ") AND " + primaryKey + " >= " + pageOptions.firstIdNotSeen
	
	buildPaginatedQuery = sql
end function

exports.getTableData = getTableData
exports.updateTableData = updateTableData

